buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.0-beta-3595'
        classpath 'gradle.plugin.org.jetbrains:gradle-intellij-plugin:0.0.23'
    }
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.intellij'

intellij {
    version '15.0'
    plugins 'kotlin'
}

sourceSets {

    main {
        kotlin {
            srcDirs = ['src']
        }
        java {
            srcDirs = ['src', 'gen']
        }
        resources {
            srcDirs = ['src/resources']
        }
    }

    test {
        kotlin {
            srcDirs = ['test']
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib:1.0.0-beta-3595'
    compile 'org.jetbrains.kotlin:kotlin-runtime:1.0.0-beta-3595'
    compile files('format.jar')
    compile files('printer-core.jar')
    testCompile 'junit:junit:+'
}

idea.project.ipr {
    beforeMerged { project ->
        project.modulePaths.clear()
    }
}

idea.module {
    name = "whileLang"
    jdkName = "1.8"
    sourceDirs += file('src')
    sourceDirs += file('gen')
    testSourceDirs += file('test')
    iml {
        withXml {
            it.node.@type = "PLUGIN_MODULE"
            it.node.appendNode('component'
                    , [name: 'DevKit.ModuleBuildProperties', url: 'file://$MODULE_DIR$/src/resources/META-INF/plugin.xml'])
        }
    }
}

defaultTasks 'test'

task setup {
    dependsOn ideaModule, ideaProject
    doLast {
        copy {
            from '.'
            into '.idea/'
            include '*.ipr'
            rename { "modules.xml" }
        }
        project.delete "${project.name}.ipr"
    }
}

compileKotlin.dependsOn(":printergenerator:run")

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

